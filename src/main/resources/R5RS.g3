/*
 * Copyright (C) 2011 by Bart Kiers, based on the work done by HÃ¥kan L. Younes'
 * JavaCC R5RS grammar, available at: http://mindprod.com/javacc/R5RS.jj
 * 
 * Permission is hereby granted, free of charge, to any person obtaining a copy
 * of this software and associated documentation files (the "Software"), to deal
 * in the Software without restriction, including without limitation the rights
 * to use, copy, modify, merge, publish, distribute, sublicense, and/or sell
 * copies of the Software, and to permit persons to whom the Software is
 * furnished to do so, subject to the following conditions:
 * 
 * The above copyright notice and this permission notice shall be included in
 * all copies or substantial portions of the Software.
 * 
 * THE SOFTWARE IS PROVIDED "AS IS", WITHOUT WARRANTY OF ANY KIND, EXPRESS OR
 * IMPLIED, INCLUDING BUT NOT LIMITED TO THE WARRANTIES OF MERCHANTABILITY,
 * FITNESS FOR A PARTICULAR PURPOSE AND NONINFRINGEMENT. IN NO EVENT SHALL THE
 * AUTHORS OR COPYRIGHT HOLDERS BE LIABLE FOR ANY CLAIM, DAMAGES OR OTHER
 * LIABILITY, WHETHER IN AN ACTION OF CONTRACT, TORT OR OTHERWISE, ARISING FROM,
 * OUT OF OR IN CONNECTION WITH THE SOFTWARE OR THE USE OR OTHER DEALINGS IN
 * THE SOFTWARE.
 */
grammar R5RS;

options {
   output=AST;
}

// output tokens
tokens {
    FORMALDEF;
    IDENTIFIER;
    COMMAND;
    PROCEDURECALL;
    SYNTAXRULE;
    LITERAL;
    SELFEVALUATING;
    QUOTATION;
    LIST;
    VECTOR;
    EMPTYFORMALS;
    FORMALS;
    SEQUENCE;
    CONDCLAUSE;
    TEST;
    BODY;
    CASECLAUSE;
    ABBREVIATION;
    QUASIQUOTATION;
    LOCALBINDING;
    BINDINGSPEC;
    ITERATIONSPEC;
    DOCOMMANDS;
    DORESULT;
    STR;
    CHAR;
    NUM2;
    NUM8;
    NUM10;
    NUM16;
    BOOL;
}


@parser::header {
package parse;
}

@lexer::header {
package parse;
}

@members {
static class VariableNode extends CommonTree {public VariableNode(Token t) {super(t);}}
}

parse
  :  commandOrDefinition* EOF
  ;

commandOrDefinition
  :  (syntaxDefinition)=>              syntaxDefinition
  |  (definition)=>                    definition
  |  ('(' BEGIN commandOrDefinition)=> '('! BEGIN commandOrDefinition+ ')'!
  |                                    command
  ;

syntaxDefinition
  :  '('! DEFINE_SYNTAX^ keyword transformerSpec ')'!
  ;

definition
  : (simpleDefinition) => simpleDefinition
  | (formalDefinition) => formalDefinition
  | '('! BEGIN^ definition* ')'!
  ;

simpleDefinition
  :  '('! DEFINE^ variable expression ')'!
  ;

formalDefinition
  : '('! DEFINE^ formals body ')'!
  ;

formals
  : '('! variable variable* ('.' variable)? ')'!
  ;

keyword
  :  identifier
  ;

/**** UNTESTED *****/
transformerSpec
  :  '('! SYNTAX_RULES^ '('! identifier* ')'! syntaxRule* ')'!
  ;

syntaxRule
  :  '(' pattern template ')' -> ^(SYNTAXRULE pattern template)
  ;

pattern
  :  patternIdentifier
  |  '(' (pattern+ ('.' pattern | ELLIPSIS)?)?  ')'
  |  '#(' (pattern+ ELLIPSIS? )? ')'
  |  patternDatum
  ;

patternIdentifier
  :  syntacticKeyword
  |  VARIABLE
  ;

patternDatum
  :  selfEvaluating
  ;

template
  :  patternIdentifier
  |  '(' (templateElement+ ('.' templateElement)?)? ')'
  |  '#(' templateElement* ')'
  |  templateDatum
  ;

templateElement
  :  template ELLIPSIS?
  ;

templateDatum
  :  patternDatum
  ;
/**** END UNTESTED ****/

command
  :  expression -> ^(COMMAND expression)
  ;

identifier
  :  syntacticKeyword -> ^(IDENTIFIER syntacticKeyword)
  |  variable -> ^(IDENTIFIER variable)
  ;

syntacticKeyword
  :  expressionKeyword
  |  ELSE
  |  ARROW
  |  DEFINE
  |  UNQUOTE
  |  UNQUOTE_SPLICING
  ;  

expressionKeyword
  :  QUOTE
  |  LAMBDA
  |  IF
  |  SET
  |  BEGIN
  |  COND
  |  AND
  |  OR
  |  CASE
  |  LET
  |  LETSTAR
  |  LETREC
  |  DO
  |  DELAY
  |  QUASIQUOTE
  ;  

expression
  :  (variable)=>          variable
  |  (literal)=>           literal -> ^(LITERAL literal)
  |  (lambdaExpression)=>  lambdaExpression
  |  (conditional)=>       conditional
  |  (assignment)=>        assignment
  |  (derivedExpression)=> derivedExpression
  |  (procedureCall)=>     procedureCall
  |  (macroUse)=>          macroUse
  |                        macroBlock
  ;

variable
  :  VARIABLE<VariableNode>^
  |  ELLIPSIS^
  ;

literal
  :  quotation -> ^(QUOTATION quotation)
  |  selfEvaluating -> ^(SELFEVALUATING selfEvaluating)
  ;

quotation
  :  '\''! datum
  |  '(' QUOTE datum ')'
  ;

selfEvaluating
  :  bool
  |  number
  |  CHARACTER -> ^(CHAR CHARACTER)
  |  STRING -> ^(STR STRING)
  ;

lambdaExpression
  :  '('! LAMBDA^ lambdaformals body ')'!
  ;

lambdaformals
  : (variable) => variable
  | (formals) => formals
  | '()' -> ^(EMPTYFORMALS)
  ;


conditional
  :  '('! IF^ test consequent alternate? ')'!
  ;

test 
  :  expression -> ^(TEST expression)
  ;

consequent  
  :  expression
  ;

alternate 
  :  expression
  ;

assignment
  :  '('! SET^ variable expression ')'!
  ;

derivedExpression
  :  quasiquotation -> ^(QUASIQUOTATION quasiquotation)
  |  '('! ( COND^ ( '('! elseClause ')'!
                  | condClause+ ('('! elseClause ')'!)?
                  )
         | CASE^ expression ( '('! elseClause ')'!
                            | caseClause+ ('('! elseClause ')'!)?
                            )
         | AND^ test*
         | OR^ test*
         | localBinding
         | BEGIN^ sequence
         | DO^ '('! iterationSpec* ')'! '('! test doResult? ')'! doCommands
         | DELAY^ expression
         ) 
     ')'!
  ;


doCommands
  : command* -> ^(DOCOMMANDS command*)
  ;

condClause
  :  '(' test condrealize? ')' -> ^(CONDCLAUSE test condrealize?)
  ;

elseClause
  : ELSE^ sequence
  ;

condrealize
  : sequence
  | ARROW^ recipient
  ;

recipient
  :  expression
  ;

caseClause
  :  '(' '(' datum* ')' sequence ')' -> ^(CASECLAUSE datum* sequence)
  ;


localBinding
  : localBindingDefinition -> ^(LOCALBINDING localBindingDefinition)
  ;

localBindingDefinition
  : LET^ variable? '('! bindingSpec* ')'! body
  | LETSTAR^ '('! bindingSpec* ')'! body
  | LETREC^ '('! bindingSpec* ')'! body
  ;

bindingSpec
  :  '(' variable expression ')' -> ^(BINDINGSPEC variable expression)
  ;

iterationSpec
  :  '(' variable init step? ')' -> ^(ITERATIONSPEC variable init step?)
  ;

init
  :  expression
  ;

step
  :  expression
  ;

doResult
  :  sequence -> ^(DORESULT sequence)
  ;

procedureCall
  :  '(' operator operand* ')' -> ^(PROCEDURECALL operator operand*)
  ;

operator
  :  expression
  ;

operand
  :  expression
  ;

macroUse
  :  '(' keyword datum* ')'
  ;

macroBlock
  :  '(' (LET_SYNTAX | LETREC_SYNTAX) '(' syntaxSpec* ')' body ')'
  ;

syntaxSpec
  :  '(' keyword transformerSpec ')'
  ;

body
  : ((definition)=> definition)* sequence -> ^(BODY definition* sequence)
  ;

sequence
  :  expression+ -> ^(SEQUENCE expression+)
  ;

datum
  :  simpleDatum
  |  compoundDatum
  ;

simpleDatum
  :  selfEvaluating
  |  identifier
  ;

compoundDatum
  :  list -> ^(LIST list)
  |  vector -> ^(VECTOR vector)
  ;

list
  :  '('! (datum+ ('.' datum)?)? ')'!
  |  abbreviation -> ^(ABBREVIATION abbreviation)
  ;

abbreviation
  :  abbrevPrefix datum
  ;

abbrevPrefix
  :  '\'' | '`' | ',@' | ','
  ;

vector
  :  '#('! datum* ')'!
  ;

number
  :  NUM_2 -> ^(NUM2 NUM_2)
  |  NUM_8 -> ^(NUM8 NUM_8)
  |  NUM_10 -> ^(NUM10 NUM_10)
  |  NUM_16 -> ^(NUM16 NUM_16)
  ;

bool
  :  TRUE -> ^(BOOL TRUE)
  |  FALSE -> ^(BOOL FALSE)
  ;

/* Unimplemented here on out) */

quasiquotation
  :  quasiquotationD[1]
  ;

quasiquotationD[int d]
  :  '`' qqTemplate[d]
  |  '(' QUASIQUOTE qqTemplate[d] ')'
  ;

qqTemplate[int d]
  :  (expression)=>  expression
  |  ('(' UNQUOTE)=> unquotation[d]
  |                  simpleDatum
  |                  vectorQQTemplate[d]
  |                  listQQTemplate[d]
  ;

vectorQQTemplate[int d]
  :  '#(' qqTemplateOrSplice[d]* ')'
  ;

listQQTemplate[int d]
  :                     '\'' qqTemplate[d]
  |  ('(' QUASIQUOTE)=> quasiquotationD[d+1]
  |                     '(' (qqTemplateOrSplice[d]+ ('.' qqTemplate[d])?)? ')'
  ;

unquotation[int d]
  :  ',' qqTemplate[d-1]
  |  '(' UNQUOTE qqTemplate[d-1] ')'
  ;

qqTemplateOrSplice[int d]
  :  ('(' UNQUOTE_SPLICING)=> splicingUnquotation[d]
  |                           qqTemplate[d]
  ;

splicingUnquotation[int d]
  :  ',@' qqTemplate[d-1]
  |  '(' UNQUOTE_SPLICING qqTemplate[d-1] ')'
  ;

// macro keywords
LET_SYNTAX       : 'let-syntax';
LETREC_SYNTAX    : 'letrec-syntax';
SYNTAX_RULES     : 'syntax-rules';
DEFINE_SYNTAX    : 'define-syntax';

// syntactic keywords
ELSE             : 'else';
ARROW            : '=>';
DEFINE           : 'define';
UNQUOTE_SPLICING : 'unquote-splicing';
UNQUOTE          : 'unquote';

// expression keywords
QUOTE            : 'quote';
LAMBDA           : 'lambda';
IF               : 'if';
SET              : 'set!';
BEGIN            : 'begin';
COND             : 'cond';
AND              : 'and';
OR               : 'or';
CASE             : 'case';
LET              : 'let';
LETSTAR          : 'let*';
LETREC           : 'letrec';
DO               : 'do';
DELAY            : 'delay';
QUASIQUOTE       : 'quasiquote';

NUM_2  : PREFIX_2 COMPLEX_2;
NUM_8  : PREFIX_8 COMPLEX_8;
NUM_10 : PREFIX_10? COMPLEX_10;
NUM_16 : PREFIX_16 COMPLEX_16;

ELLIPSIS : '...';

VARIABLE 
  :  INITIAL SUBSEQUENT* 
  |  PECULIAR_IDENTIFIER
  ;

STRING : '"' STRING_ELEMENT* '"';

CHARACTER : '#\\' (~(' ' | '\n') | CHARACTER_NAME);

TRUE  : '#' ('t' | 'T');
FALSE : '#' ('f' | 'F');

// to ignore
SPACE   : (' ' | '\t' | '\r' | '\n') {$channel=HIDDEN;};
COMMENT : ';' ~('\r' | '\n')* {$channel=HIDDEN;};

// fragments  
fragment INITIAL : LETTER | SPECIAL_INITIAL;
fragment LETTER : 'a'..'z' | 'A'..'Z';
fragment SPECIAL_INITIAL : '!' | '$' | '%' | '&' | '*' | '/' | ':' | '<' | '=' | '>' | '?' | '^' | '_' | '~';
fragment SUBSEQUENT : INITIAL | DIGIT | SPECIAL_SUBSEQUENT;
fragment DIGIT : '0'..'9';
fragment SPECIAL_SUBSEQUENT : '.' | '+' | '-' | '@';
fragment PECULIAR_IDENTIFIER : '+' | '-';
fragment STRING_ELEMENT : ~('"' | '\\') | '\\' ('"' | '\\');
fragment CHARACTER_NAME : 'space' | 'newline';

fragment COMPLEX_2 
  :  REAL_2 ('@' REAL_2)?
  |  REAL_2? SIGN UREAL_2? ('i' | 'I')
  ;

fragment COMPLEX_8 
  :  REAL_8 ('@' REAL_8)?
  |  REAL_8? SIGN UREAL_8? ('i' | 'I')
  ;

fragment COMPLEX_10 
  :  REAL_10 ('@' REAL_10)?
  |  REAL_10? SIGN UREAL_10? ('i' | 'I')
  ;

fragment COMPLEX_16 
  :  REAL_16 ('@' REAL_16)?
  |  REAL_16? SIGN UREAL_16? ('i' | 'I')
  ;

fragment REAL_2 : SIGN? UREAL_2;
fragment REAL_8 : SIGN? UREAL_8;
fragment REAL_10 : SIGN? UREAL_10;
fragment REAL_16 : SIGN? UREAL_16;
fragment UREAL_2 : UINTEGER_2 ('/' UINTEGER_2)?;
fragment UREAL_8 : UINTEGER_8 ('/' UINTEGER_8)?;
fragment UREAL_10 : UINTEGER_10 ('/' UINTEGER_10)? | DECIMAL_10;
fragment UREAL_16 : UINTEGER_16 ('/' UINTEGER_16)?;

fragment DECIMAL_10 
  :  UINTEGER_10 SUFFIX
  |  '.' DIGIT+ '#'* SUFFIX?
  |  DIGIT+ '.' DIGIT* '#'* SUFFIX?
  |  DIGIT+ '#'+ '.' '#'* SUFFIX?
  ;

fragment UINTEGER_2 : DIGIT_2+ '#'*;
fragment UINTEGER_8 : DIGIT_8+ '#'*;
fragment UINTEGER_10 : DIGIT+ '#'*;
fragment UINTEGER_16 : DIGIT_16+ '#'*;
fragment PREFIX_2 : RADIX_2 EXACTNESS? | EXACTNESS RADIX_2;
fragment PREFIX_8 : RADIX_8 EXACTNESS? | EXACTNESS RADIX_8;
fragment PREFIX_10 : RADIX_10 EXACTNESS? | EXACTNESS RADIX_10;
fragment PREFIX_16 : RADIX_16 EXACTNESS? | EXACTNESS RADIX_16;
fragment SUFFIX : EXPONENT_MARKER SIGN? DIGIT+;
fragment EXPONENT_MARKER : 'e' | 's' | 'f' | 'd' | 'l' | 'E' | 'S' | 'F' | 'D' | 'L';
fragment SIGN : '+' | '-';
fragment EXACTNESS : '#' ('i' | 'e' | 'I' | 'E');
fragment RADIX_2 : '#' ('b' | 'B');
fragment RADIX_8 : '#' ('o' | 'O');
fragment RADIX_10 : '#' ('d' | 'D');
fragment RADIX_16 : '#' ('x' | 'X');
fragment DIGIT_2 : '0' | '1';
fragment DIGIT_8 : '0'..'7';
fragment DIGIT_16 : DIGIT | 'a'..'f' | 'A'..'F';